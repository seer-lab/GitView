% Overrides to preserve comments in Java
% Jim Cordy, March 2001 (Rev Feb 2003)

#pragma -comment

% Can have comments as statements 
%redefine statement
%	...
%end redefine

% redefined the comments to be attached to statements rather then to be a statement of their own
%define com_statement
%	[opt comment_block_NL][statement]
%end define

define com_statement
	        [label_statement]        
    |   [empty_statement]
    |   [expression_statement]
    |   [if_statement]
    |   [switch_statement]
    |   [while_statement]
    |   [do_statement]
    |   [for_statement]
    |   [for_in_statement]    % T.D. Oct 2007
    |   [break_statement]
    |   [continue_statement]
    |   [return_statement]
    |   [throw_statement]
    |   [synchronized_statement]
    |   [try_statement]
    |   [assert_statement]    % T.D. Oct 2007
    |   [block]
end define

redefine statement
	[opt comment_block_NL] [com_statement]
end redefine

define com_declaration
	[local_variable_declaration] 
    |   [class_declaration]
end define

redefine declaration
	[opt comment_block_NL][com_declaration]
end redefine

%define com_declaration
%	[opt comment_block_NL][declaration]
%end define

%redefine declaration_or_statement
%	[com_declaration]
%    |   [statement]
%end redefine

redefine instance_initializer
    [NL] [opt comment_block_NL] [block]     [NL][NL]
end redefine

redefine static_initializer
    [NL] [opt comment_block_NL] 'static [block]     [NL][NL]
end redefine
  
% Need to change this to allow for comments linked to the different declarations
%Including
% 	- empty_declaration (might not be needed)
% Can have comments as declarations
%redefine class_body_declaration
%	... 
%|	[comment_block_NL]
%end redefine

redefine package_declaration
	[opt comment_block_NL] ...
end redefine

%Changed to be prior since comments were grabbed to greedily
redefine import_declaration
	[opt comment_block_NL] ...
end redefine

redefine type_declaration
	[opt comment_block_NL] ...
end redefine

redefine variable_declaration
	[opt comment_block_NL] ...
end redefine

% Can have comments before initializers ...
redefine variable_initializer
	[opt comment_block_NL] ...
end redefine

% ... or after initializers ...
redefine variable_initializer
	... [opt comment_block_NL]
end redefine

% ... or before =initializers 
redefine equals_variable_initializer
	[opt comment_block_NL] ...
end redefine

% Can have comments before formal parameters ...
redefine formal_parameter
	[opt comment_block_NL] ...
end redefine

% ... or after formal parameters ...
redefine formal_parameter
	...  [opt comment_block_NL]
end redefine

redefine constructor_declaration
	[opt comment_block_NL] ...
end redefine

redefine method_declaration
	[opt comment_block_NL] ...
end redefine

% ... or after a parameter list 
redefine constructor_declarator
	... [opt comment_block_NL]
end redefine

redefine method_declarator
	... [opt comment_block_NL]
end redefine

% Can have comments before arguments ...
redefine argument
	[opt comment_block_NL] ...
end redefine

% ... or after arguments ...
redefine argument
	...  [opt comment_block_NL]
end redefine

% ... or after an argument list 
redefine method_argument
	... [opt comment_block_NL]
end redefine

% Can have comments after an if condition
redefine if_statement
    'if '( [expression] ')	[opt comment_block_NL]
		[statement]
	[opt else_clause]   [NL]
end redefine

%Possible issue is that now a block could have a comment prior to it (as per statment) and then one before it as per the redefine of the comment grammar

% Can have comments on a block
redefine block
	[opt comment_block_NL] ...
end redefine

% Can have comments before an expression
redefine expression
	[opt comment_block_NL] ...
end redefine

% Need newlines after comments
define comment_NL
	[comment] [NL]
end define

define comment_block_NL
	[opt comment_start_tag]  [comment_NL] [repeat comment_NL]
	[opt comment_end_tag]
end define

define comment_block
    [opt comment_start_tag] [comment_NL]
    [opt comment_end_tag]
end define

define comment_start_tag
	'< 'COMMENT [opt tag_type] [opt tag_value] '>
end define

define tag_type
	'type = [stringlit] 
end define

define tag_value
	'value = [stringlit]
end define


define comment_end_tag
	'</ 'COMMENT '> [NL]
end define
